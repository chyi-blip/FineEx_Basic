// 这里添加下面两行代码。
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/cy757120553/FineEx_Basic/tree/master' // 项目主页。
def gitUrl = 'https://github.com/cy757120553/FineEx_Basic.git' // Git仓库的url。
// 唯一包名，比如implementation 'groupId:artifactId:version'中的groupId就是这里配置的。
group = "com.fineex"
//// 项目名称, 比如implementation 'groupId:artifactId:version'中的artifactId就是这里配置的。
project.archivesBaseName = "basic"
//项目引用的版本号，比如implementation 'groupId:artifactId:version'中的version就是这里配置的。
version = "1.0.0"

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar' // 包格式aar、jar
                // 项目描述
                name 'basic'// 可选，项目名称。
                description 'FineEx basic'// 可选，项目描述。
                url siteUrl // 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，可以不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，填写你bintray网站上个人信息，上面步骤有提过。
                developers {
                    developer {
                        id 'cy757120553' // 开发者的id。
                        name '陈熠' // 开发者名字。
                        email 'chying0108@gmail.com' // 开发者邮箱。
                    }
                }

                // 可以不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}

// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，
// 所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "maven"  //Repository名字 需要自己在bintray网站上先添加
        name = "basic"// 发布到Bintray上的项目名字,你在bintray网站上新建的包名
        userOrg = 'cy757120553'//Bintray的组织id，可以填写的你的开发者的id
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}